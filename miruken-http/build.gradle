apply from: "${rootDir}/gradle/kotlin.gradle"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
    implementation 'com.squareup.retrofit2:retrofit:2.8.0'
    api "com.miruken:miruken:$miruken_version"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.4.1'
}

def artifactName = 'miruken-http'

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set("sources")
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact   sourcesJar
            groupId    mirukenGroupId
            artifactId artifactName
            pom.withXml {
                def properties = asNode().appendNode('properties')
                properties.appendNode('maven.compiler.source', '1.8')
                properties.appendNode('maven.compiler.target', '1.8')
            }
        }
    }
}

bintray {
    user         = System.getenv('BINTRAY_USER')
    key          = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    publish      = true
    pkg {
        name     = artifactName
        userOrg  = mirukenOrg
        repo     = mirukenRepo
        licenses = [mirukenLicense]
        vcsUrl   = mirukenVcsUrl
        version {
            released = new Date()
        }
    }
}
